package org.verapdf.model.pdlayer;

import org.verapdf.model.baselayer.Object;
import org.verapdf.model.coslayer.CosDict;
import org.verapdf.model.coslayer.CosStream;
import org.verapdf.model.coslayer.CosReal;
import org.verapdf.model.coslayer.CosRenderingIntent;

import org.verapdf.model.operator.Operator;
import org.verapdf.model.external.ICCOutputProfile;
import org.verapdf.model.external.ICCInputProfile;
import org.verapdf.model.xmplayer.XMPPackage;


% base class for all PD layer objects	
type PDObject extends Object {
	
}

% the class corresponding to the high level PDF document structure 
type PDDocument extends PDObject {
	% link to all embedded file specifications
	link EmbeddedFiles: CosFileSpecification;
	% the collection of all document pages
	link pages: PDPage+;
	% document level metadata
	link metadata: PDMetadata?;
	% the array of output intents of the document
	link outputIntents: PDOutputIntent*;
	% Interactive Form Dictionary (AcroForm entry in the Catalog)
	link AcroForm: PDAcroForm?;
	% additional actions defined for the document (AA entry in the Catalog)
	link AA: PDAction*;
}

% the object representing PDF page	
type PDPage extends PDObject {
	% all page annotations
	link annots: PDAnnot*;
	% document level action
	link action: PDAction?;
	% page content stream
	link contentStream: PDContentStream;
	% group attributes dictionary
	link Group: PDGroup?;
}

% the object representing content stream together with the resource dictionary
type PDContentStream extends PDObject {
	% all operators in the content stream
	link operators: Operator*;
}

% PDF resource object
type PDResource extends PDObject {

}

% Color space object
type PDColorSpace extends PDResource {
	% number of components
	property nrComponents: Integer;
}

% DeviceGray color space
type PDDeviceGray extends PDColorSpace {

}

% DeviceRGB color space
type PDDeviceRGB extends PDColorSpace {

}

% DeviceCMYK color space
type PDDeviceCMYK extends PDColorSpace {

}

% ICCBased color space
type PDICCBased extends PDColorSpace {
	% embedded ICC profile
	link iccProfile: ICCInputProfile;
}

% Lab color space
type PDLab extends PDColorSpace {

}

% CalGray color space
type PDCalGray extends PDColorSpace {

}

% CalRGB color space
type PDCalRGB extends PDColorSpace {

}

% Separation color space
type PDSeparation extends PDColorSpace {
	% alternate color space
	link alternate: PDColorSpace;
}

% DeviceN color space
type PDDeviceN extends PDColorSpace {
	% alternate color space
	link alternate: PDColorSpace;
}

% Indexed color space
type PDIndexed extends PDColorSpace {
	% base color space
	link base: PDColorSpace;
}

% generic Pattern type
type PDPattern extends PDResource {

}

% tiling Pattern
type PDTilingPattern extends PDPattern {
	% content stream of the pattern
	link contentStream: PDContentStream;
}

% shading Pattern
type PDShadingPattern extends PDPattern {
	% shading object
	link shading: PDShading;
}

% XObject resource
type PDXObject extends PDResource {
	% subtype of the XObject resource (Image, Form, PS)
	property Subtype: String;
	% link to the OPI dictionary (OPI key in the XObject dictionary)
	link OPI: CosDict?;
	% link to the soft mask image (SMask key)
	% according to the PDF Specification this key may be present only in the Image dictionary, 
	% but ISO 19005-1:2005/Cor.2:2011 forbids SMask key for all XObject dictionaries
	link SMask: PDXObject?;
}

% Image XObject
type PDXImage extends PDXObject {
	% image color space
	link imageCS: PDColorSpace;
	% Interpolate key in the image dictionary
	property Interpolate: Boolean;
	% image alternates (Alternates key in Image dictionary)
	link Alternates: PDXImage*;
}

% Form XObject
type PDXForm extends PDXObject {
	% value of the Subtype2 key
	property Subtype2: String;
	% link to the PostScript stream (PS key in PDF 1.0)
	link PS: CosStream?;
	% link to the reference XObject (Ref key in the form XObject dictionary)
	link Ref: CosDict?;
	% form content
	link contentStream: PDContentStream;
	% link to the group attributes dictionary
	link Group: PDGroup;
}

% Group attributes dictionary
type PDGroup extends PDObject {
	% group subtype (S key)
	property S: String;
}

% ExtGState dictionary
type PDExtGState extends PDResource {
	% String representation of the TR key ("Identity" or "Custom")
	property TR: String;
	% String representation of the TR2 key ("Identity", "Default" or "Custom")
	property TR2: String;
	% String representation of the SMask key ("None" or "Custom")
	property SMask: String;
	% blend mode
	property BM: String;
	% fill alpha
	property ca: Decimal;
	% stroke alpha
	property CA: Decimal;
	% the rendering intent
	link RI: CosRenderingIntent?;
}

% Shading resource
type PDShading extends PDResource {
	% shading color space
	link colorSpace: PDColorSpace;
}

% annotation object	
type PDAnnot extends PDObject {
	% annotation subtype
	property Subtype: String;
	% annotation opacity (CA key)
	property CA: Decimal;
	% annotation flags
	property F: Integer;
	% SPACE-separated list of all keys in the annotation's appearance dictionary (AP key)
	property AP: String;
	% normal appearance stream (N key in the appearance dictionary) of the annotation
	link appearance: PDContentStream?;
	% link to the annotation's color
	link C: CosReal*;
	% link to the annotation's interior color
	link IC: CosReal*;
	% link to the action associated with the annotation (A entry)
	link A: PDAction?;
	% link to the additional actions defined for the annotation (AA entry)
	link AA: PDAction*;
}

% Interactive form dictionary
type PDAcroForm extends PDObject {
	% NeedAppearances flag
	property NeedAppearances: Boolean;
	% all interactive form fields in the document
	link formFields: PDFormField*;
}

% Interactive form field
type PDFormField extends PDObject {
	% Field type
	property FT: String;
	% link to the additional actions defined in the interactive form field dictionary (AA entry)
	link AA: PDAction*;	
}
	
% the PDF action object
type PDAction extends PDObject {
	% action subtype
	property S: String;
}

% the Named action object
type PDNamedAction extends PDAction {
	% name of the action
	property N: String;
}

% the XMP Metadata stream
type PDMetadata extends PDObject {
	% XMPPackage for PDMetadata in CosDict or XMPMainPackage for PDMetadata in PDDocument
	link XMPPackage: XMPPackage;
	% link to the CosStream contains this metadata
	link stream: CosStream;
	% value of the Filter key in the metadata stream dictionary
	property Filter: String;
}

% the output intent dictionary
type PDOutputIntent extends PDObject {
	% destination output profile
	link destProfile: ICCOutputProfile?;
}